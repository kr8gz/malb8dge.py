memory = [ptr = inputPtr = 0] * 30000
input = _("Input: ")
brainfuck = (code): {
    codePtr = 0
    codePtr < code_ ~ ? {
        char = code.codePtr
        "+-"[?char] ? {
            memory.ptr = memory.ptr + (char + 1)$ % 256
        } ! "<>"[?char] ? {
            ptr = ptr + (char == ">" ? 1 ! -1) % 30000
        } ! char == "." ? {
            |'memory.ptr
        } ! char == "," ? {
             memory.ptr = inputPtr < input_ ? 'input.inputPtr++
        } ! char == "[" ? {
            end = codePtr
            brackets = 0
            (^^code)[codePtr:] ~ i, c {
                end = i
                c == "[" ? {
                    brackets++
                }
                c == "]" ? {
                    !--brackets ? {
                        %
                    }
                }
            }
            brackets ? {
                ;"Unbalanced brackets!"
                %%
            }
            memory.ptr ~ ? {
                brainfuck(code[codePtr + 1:end])
            }
            codePtr = end
        } ! char == "]" ? {
            ;"Unbalanced brackets!"
            %%
        }
        codePtr++
    }
}

brainfuck("++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++++++.>.+++.------.--------.>+.>.")